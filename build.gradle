plugins {
    id 'java'
    id 'maven-publish'
    id('com.github.hierynomus.license') version '0.14.0'
}

group 'io.github.mrblobman'
version '1.3-SNAPSHOT'

wrapper {
    gradleVersion = '4.8.1'
    distributionType = Wrapper.DistributionType.ALL
}

// Add the license header to source files
license {
    include '**/*.java'
    header = rootProject.file('LICENSE')
    strictCheck true
    mapping {
        // Use a regular multiline comment rather than a javadoc comment
        java = 'SLASHSTAR_STYLE'
    }
}
build.dependsOn 'licenseFormat'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    //Spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    //Bungee
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    //Rest of the libs
    mavenCentral()
}

dependencies {
    compile group: 'org.spigotmc', name: 'spigot-api', version: '1.8.8-R0.1-SNAPSHOT'
}

sourceSets {
    api {
        java {
            compileClasspath += configurations.compile
            srcDir 'src/main/java'
            exclude 'io/github/mrblobman/spigotcommandlib/args/**'
            exclude 'io/github/mrblobman/spigotcommandlib/registry/**'
        }
    }
}

task testJar(type: Jar) {
    classifier = 'test'
    from sourceSets.main.output
    from sourceSets.test.output
}

task apiDoc(type: Javadoc) {
    source = sourceSets.api.allJava
    destinationDir = reporting.file("apiDocs")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}